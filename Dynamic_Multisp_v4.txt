### DYNAMIC_MULTIS.TXT

### THIS IS THE MODEL FOR THE LADYBIRDS MULTI-SPECIES DYNAMIC OCCUPANCY MODEL. 

### THIS COMES FROM NICK'S DYNAMIC_MULTISPNNI_160418.TXT

model {                       


###############################  State model Priors 

# for species terms where all species are drawn from a common distribution
for(i in 1:nspecies){
  # FIXED EFFECTS
  init.occ[i] ~ dunif(0, 1) # vague prior on occupancy in year 1. 

  # RANDOM EFFECTS
  beta0.phi[i] ~ dnorm(mu.beta0.phi, tau.beta0.phi)
  logitgamma[i] ~ dnorm(mu.gamma, tau.gamma) # Colonisation probabilities
  logit(gamma[i]) <- logitgamma[i]

  # EFFECTS THAT ARE RANDOM BUT DEPENDENT ON PRESENCE OF HARLEQUIN
  #beta1[i] ~ dnorm(mu.beta1[[i]], tau.beta1[[i]])
  beta1[i] ~ dnorm(mu.beta1, tau.beta1) 
 }

# for (t in 1:nyear) {
#  alpha.phi[t] ~ dnorm(0, tau.alpha.phi)
# }



### State model hyperpriors

mu.beta0.phi ~ dnorm(0, 0.01) #persistence intercept (expected value=0, so persistence=0.5)
mu.gamma ~ dnorm(0, 0.01)
sd.beta0.phi ~ dt(0,1,1)T(0,)
sd.gamma ~ dt(0,1,1)T(0,)
tau.beta0.phi <- 1/(sd.beta0.phi * sd.beta0.phi)
tau.gamma <- 1/(sd.gamma * sd.gamma)

beta1.mean ~ dunif(0,1) # parameter on the measurement scale
mu.beta1 <- logit(beta1.mean)
sd.beta1 ~ dt(0,1,1)T(0,)
tau.beta1 <- 1/(sd.beta1 * sd.beta1)

# sd.alpha.phi ~ dt(0,1,1)T(0,)     
# tau.alpha.phi <- 1/(sd.alpha.phi * sd.alpha.phi)

############################### Observation model Priors 

for(i in 1:nspecies){
  beta2.p[i] ~ dnorm(mu.beta2.p, tau.beta2.p) 
  beta3.p[i] ~ dnorm(mu.beta3.p, tau.beta3.p)
  beta4.p[i] ~ dnorm(mu.beta4.p, tau.beta4.p)
}


for (t in 1:nyear) {
  alpha.p[t] ~ dnorm(0, tau.alpha.p)
alpha.phi[t] ~ dnorm(0, tau.alpha.phi)
}


# Observation model hyperpriors

# we could put all of these except mu.alpha.t.p back on the measurement scale
mu.beta2.p ~ dnorm(-2.4, 0.01) # mean species effect ilt(-2.4) = 0.08 (probability of being recorded on a list of length 1) 
mu.beta3.p ~ dnorm(0, 0.01)
mu.beta4.p ~ dnorm(0, 0.01)
sd.beta2.p ~ dt(0,1,1)T(0,)            
sd.beta3.p ~ dt(0,1,1)T(0,)            
sd.beta4.p ~ dt(0,1,1)T(0,) 
sd.alpha.p ~ dt(0,1,1)T(0,) 
sd.alpha.phi ~ dt(0,1,1)T(0,) 
tau.beta2.p <- 1/(sd.beta2.p * sd.beta2.p)
tau.beta3.p <- 1/(sd.beta3.p * sd.beta3.p)
tau.beta4.p <- 1/(sd.beta4.p * sd.beta4.p)
tau.alpha.p <- 1/(sd.alpha.p * sd.alpha.p)
tau.alpha.phi <- 1/(sd.alpha.phi * sd.alpha.phi)
  

### Need to change to express the SD as half-cauchy and then define precision as 1/sd^2       
     

############################### 


# State model
# indexing changed to match Ruiz-Gutierrez et al
for (i in 1:nspecies){ 
 for (j in 1:nsite){ # j loop indexes the monads/sites
  z[i,j,1] ~ dbern(init.occ[i])
  
 for (t in 2:nyear){   
    # Persistence at site i in year t is a function of covariates in year t-1
    logit(phi[i,j,t]) <- alpha.phi[t] + beta0.phi[i] + beta1[i] * HARL[j,t-1] 
    
    # Dynamic Occupancy = previous occupancy, modified by persistence + colonization
    muZ[i,j,t] <- z[i,j,t-1] * phi[i,j,t] + (1 - z[i,j,t-1]) * gamma[i]
    
    # True occupancy z at site i in year t
    z[i,j,t] ~ dbern(muZ[i,j,t])
}}}   


# Observation model: go through the visits and find the matching year and site identity
# now indexed by k, rather than j
for (i in 1:nspecies){ 
  for(k in 1:nvisit) {
  #for each visit, find the matching site, quadrant and year identities
  logit(p[i,k]) <- alpha.p[Year[k]] + beta2.p[i]  + beta3.p[i]*DATATYPE2[k] + beta4.p[i]*DATATYPE3[k] 

  Py[i,k] <- z[i,Site[k],Year[k]] * p[i,k]  # Site refers to the monad in [Site[k]]
  
  y[k,i] ~ dbern(Py[i,k])  
}}


# end of model formulation
}      		
